group 'me.yoryor.app'
version '1.0-SNAPSHOT'

apply plugin: 'java'

ext {
 vertxStack = "3.3.3"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
  compile "io.vertx:vertx-core:${vertxStack}"
  compile "io.vertx:vertx-web:${vertxStack}"
  compile "io.vertx:vertx-redis-client:${vertxStack}"
  compile "io.vertx:vertx-jdbc-client:${vertxStack}"

  compileOnly "io.vertx:vertx-codegen:${vertxStack}"

  testCompile "io.vertx:vertx-unit:${vertxStack}"
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

task annotationProcessing(type: JavaCompile, group: 'build') {
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.compileOnly
  destinationDir = project.file('src/main/generated')
  options.compilerArgs = [
          "-proc:only",
          "-processor", "io.vertx.codegen.CodeGenProcessor",
          "-AoutputDirectory=${destinationDir.absolutePath}"
  ]
}

jar {
  archiveName = 'vtodo-fat.jar'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  manifest {
    attributes 'Main-Class': 'io.vertx.core.Launcher'
    attributes 'Main-Verticle': 'me.yoryor.app.vtodo.verticles.TodoVerticle'
  }
}

sourceSets {
  main {
    java {
      srcDirs += 'src/main/generated'
    }
  }
}

compileJava {
  targetCompatibility = 1.8
  sourceCompatibility = 1.8

  dependsOn annotationProcessing
}
